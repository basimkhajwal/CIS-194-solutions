{-# LANGUAGE MonadComprehensions, RecordWildCards #-}
{-# OPTIONS_GHC -Wall #-}
module HW07 where

import Prelude hiding (mapM)
import Cards

import Control.Monad hiding (mapM, liftM)
import Control.Monad.Random
import Data.Functor
import Data.Monoid
import Data.Vector (Vector, cons, (!), (!?), (//))
import System.Random

import qualified Data.Vector as V

-- Exercise 1 -----------------------------------------

liftM :: Monad m => (a -> b) -> m a -> m b
liftM f m = m >>= (return . f)

swapV :: Int -> Int -> Vector a -> Maybe (Vector a)
swapV a b v = liftM2 takeParams (v !? a) (v !? b)
    where takeParams valA valB = v // [(a, valB),  (b, valA)]

-- Exercise 2 -----------------------------------------

mapM :: Monad m => (a -> m b) -> [a] -> m [b]
mapM _ [] = return []
mapM m (a:as) = liftM2 (:) (m a) (mapM m as)

getElts :: [Int] -> Vector a -> Maybe [a]
getElts xs v = mapM (v !?) xs

-- Exercise 3 -----------------------------------------

type Rnd a = Rand StdGen a

randomElt :: Vector a -> Rnd (Maybe a)
randomElt v = liftM (v !?) (getRandomR (0, V.length v - 1))

-- Exercise 4 -----------------------------------------

randomVec :: Random a => Int -> Rnd (Vector a)
randomVec n = liftM (foldr cons V.empty . take n) getRandoms

randomVecR :: Random a => Int -> (a, a) -> Rnd (Vector a)
randomVecR n r = liftM (foldr cons V.empty . take n) (getRandomRs r)

-- Exercise 5 -----------------------------------------

shuffle :: Vector a -> Rnd (Vector a)
shuffle v | V.length v <= 1 = return v
shuffle v = do
    i <- getRandomR (1, V.length v - 1)
    s <- shuffle (V.tail v // [(i - 1, V.head v)])
    return $ cons (v ! i) s

-- Exercise 6 -----------------------------------------

partitionAt :: Ord a => Vector a -> Int -> (Vector a, a, Vector a)
partitionAt v n = (fst partitioned, element, snd partitioned)
    where element = v ! n
          newV = V.concat [V.slice 0 n v, V.slice (n+1) (V.length v - n - 1) v]
          partitioned = V.partition ((==GT) . compare element) newV

-- Exercise 7 -----------------------------------------



-- Quicksort
quicksort :: Ord a => [a] -> [a]
quicksort [] = []
quicksort (x:xs) = quicksort [ y | y <- xs, y < x ]
                   <> (x : quicksort [ y | y <- xs, y >= x ])

qsort :: Ord a => Vector a -> Vector a
qsort v | V.length v < 2 = v
qsort v = let (s, e, l) = partitionAt v 0 in V.concat [qsort s, V.cons e (qsort l)]

-- Exercise 8 -----------------------------------------

qsortR :: Ord a => Vector a -> Rnd (Vector a)
qsortR v | V.length v < 2 = return v
qsortR v = do
    index <- getRandomR (0, V.length v - 1)
    let (s, e, l) = partitionAt v index
    smaller <- qsortR s
    larger <- qsortR l
    return $ V.concat [smaller, V.cons e larger]

-- Exercise 9 -----------------------------------------

-- Selection
select :: Ord a => Int -> Vector a -> Rnd (Maybe a)
select n v | V.length v == 1 = return (if n == 0 then Just (V.head v) else Nothing)
select n v = do
    index <- getRandomR (0, V.length v - 1)
    let (s, e, l) = partitionAt v index

    case compare n index of
        LT -> select n s
        GT -> select (n - index - 1) l
        EQ -> return $ Just e

-- Exercise 10 ----------------------------------------

allCards :: Deck
allCards = [ Card label suit | suit <- suits, label <- labels ]

newDeck :: Rnd Deck
newDeck =  shuffle allCards

-- Exercise 11 ----------------------------------------

nextCard :: Deck -> Maybe (Card, Deck)
nextCard d | V.null d = Nothing
nextCard d = Just (V.head d, V.tail d)

-- Exercise 12 ----------------------------------------

getCards :: Int -> Deck -> Maybe ([Card], Deck)
getCards n _ | n <= 0 = Nothing
getCards 1 d = nextCard d
getCards n d = do
    (card, tailD) <- nextCard d
    (taken, leftOver) <- getCards (n - 1) tailD
    return (card:taken, leftOver)

-- Exercise 13 ----------------------------------------

data State = State { money :: Int, deck :: Deck }

repl :: State -> IO ()
repl s@State{..} | money <= 0  = putStrLn "You ran out of money!"
                 | V.null deck = deckEmpty
                 | otherwise   = do
  putStrLn $ "You have \ESC[32m$" ++ show money ++ "\ESC[0m"
  putStrLn "Would you like to play (y/n)?"
  cont <- getLine
  if cont == "n"
  then putStrLn $ "You left the casino with \ESC[32m$"
           ++ show money ++ "\ESC[0m"
  else play
    where deckEmpty = putStrLn $ "The deck is empty. You got \ESC[32m$"
                      ++ show money ++ "\ESC[0m"
          play = do
            putStrLn "How much do you want to bet?"
            amt <- read <$> getLine
            if amt < 1 || amt > money
            then play
            else
              case getCards 2 deck of
                Just ([c1, c2], d) -> do
                  putStrLn $ "You got:\n" ++ show c1
                  putStrLn $ "I got:\n" ++ show c2
                  case () of
                    _ | c1 >  c2  -> repl $ State (money + amt) d
                      | c1 <  c2  -> repl $ State (money - amt) d
                      | otherwise -> war s{deck = d} amt
                _ -> deckEmpty
          war (State m d) amt = do
            putStrLn "War!"
            case getCards 6 d of
              Just ([c11, c21, c12, c22, c13, c23], d') -> do
                putStrLn $ "You got\n" ++ ([c11, c12, c13] >>= show)
                putStrLn $ "I got\n" ++ ([c21, c22, c23] >>= show)
                case () of
                  _ | c13 > c23 -> repl $ State (m + amt) d'
                    | c13 < c23 -> repl $ State (m - amt) d'
                    | otherwise -> war (State m d') amt
              _ -> deckEmpty

main :: IO ()
main = evalRandIO newDeck >>= repl . State 100
